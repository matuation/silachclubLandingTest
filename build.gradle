plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

group = 'com.matuation'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def selenideVersion = "7.13.0",
    allureVersion = "2.32.0",
    junitVersion = "5.14.1",
    slf4jVersion = "2.0.17",
    javaFakerVersion = "1.0.2",
    ownerVersion = "1.0.12",
    assertjVersion = "3.23.1"

allure {
    report {
        version.set(allureVersion)
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set(allureVersion) //версия интеграции фреймворка и Allure
            }
        }
    }
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "com.github.javafaker:javafaker:$javaFakerVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "org.assertj:assertj-core:$assertjVersion")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()
    // Прокидываем системные свойства (для Owner)
    systemProperties(System.getProperties())

    // Принудительная кодировка UTF-8
    systemProperty "file.encoding", "UTF-8"
    jvmArgs "-Dfile.encoding=UTF-8"

    testLogging {
        showStandardStreams = true
        lifecycle {
            events "started", "skipped", "failed"
            exceptionFormat "full"
        }
    }
}



